//Android Studio系列教程四--Gradle基础
//http://stormzhang.com/devtools/2014/12/18/android-studio-tutorial4/
//http://blog.csdn.net/chdjj/article/details/44975037

// 声明是Android程序
apply plugin: 'com.android.application'

android {
    // 编译SDK的版本

    compileSdkVersion 23
    // build tools的版本
    buildToolsVersion "23.0.2"
    defaultConfig {
        //默认配置，会同时应用到debug和release版本上
        // 应用的包名
        applicationId "com.lewis.lewisframe"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        //multiDexEnabled true

        // 默认是umeng的渠道
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "umeng"]
    }
    //签名配置
    signingConfigs {
        release {
            storeFile file(RELEASE_STOREFILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }
    // java版本
    /*compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }*/
    buildTypes {
        //这里面可以配置debug和release版本的一些参数，比如混淆、签名配置等
        debug {// debug模式
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"
            //Zipalign优化
            zipAlignEnabled false
            //移除无用的resource文件
            shrinkResources false
            minifyEnabled false
            //编译出来的debug版本直接用的是正式签名
            signingConfig signingConfigs.release
        }

        release {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            zipAlignEnabled true
            // 是否进行混淆
            minifyEnabled false
            shrinkResources true
            // 混淆文件的位置
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    // 移除lint检查的error
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false

    }
    productFlavors {

        wandoujia {}
        baidu {}
        c360 {}
        uc {}

        productFlavors.all { flavor ->
            flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
        }

    }
    sourceSets { main { assets.srcDirs = ['src/main/assets', 'src/main/assets/'] } }
}

dependencies {
    // 编译libs目录下的所有jar包
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile project(':library')
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    // or 1.4-beta1
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    // or 1.4-beta1
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    // or 1.4-beta1
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:gridlayout-v7:23.3.0'
    compile 'com.android.support:support-v4:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'eu.the4thfloor.volley:com.android.volley:2015.05.28'
    compile 'org.xutils:xutils:3.3.16'
    compile 'com.android.support:cardview-v7:23.3.0'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.github.bumptech.glide:glide:3.6.0'
    compile 'me.iwf.photopicker:PhotoPicker:0.2.9@aar'
    compile 'com.squareup.picasso:picasso:2.5.1'
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.1.7'
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'jp.wasabeef:glide-transformations:2.0.1'
}


